apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.taller.tp.foodie"
        minSdkVersion 23
        //noinspection OldTargetApi
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        def map_api_key = System.getenv("MAP_API_KEY") == null ? "" : System.getenv("MAP_API_KEY")
        manifestPlaceholders = [MAP_API_KEY: map_api_key]
    }
    signingConfigs {
        release
    }

    lintOptions {
        textReport = true
        abortOnError true
        warningsAsErrors true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        customDebugType {
            debuggable true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }

    def isRunningOnTravis = System.getenv("CI") == "true"

    if (isRunningOnTravis) {
        // configure keystore
        signingConfigs.release.storeFile = file("./keystore.jks")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.0.2'
    //noinspection GradleDependency
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.android.volley:volley:1.1.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.0.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'org.robolectric:robolectric:4.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'pub.devrel:easypermissions:1.1.1'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // Authentication
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.facebook.android:facebook-login:5.5.1'

    // Image Caching
    implementation 'com.facebook.fresco:fresco:2.0.0'

    // Image Cropper
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    // Firebase Messaging
    implementation 'com.google.firebase:firebase-messaging:20.0.0'

    // GSON
    implementation 'com.google.code.gson:gson:2.8.6'
}

// Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'